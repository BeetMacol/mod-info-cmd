plugins {
    id 'fabric-loom' version '0.6-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = 'mod-info-cmd'
group = 'com.beetmacol.mc'
version = '0.0.1'

Set<String> apiModules = [
        "fabric-api-base",
        "fabric-command-api-v1"
]

dependencies {
    minecraft "com.mojang:minecraft:21w14a"
    mappings "net.fabricmc:yarn:21w14a+build.15:v2"
    modImplementation "net.fabricmc:fabric-loader:0.11.3"

    apiModules.forEach {
        include(modImplementation(fabricApi.module(it, "0.32.7+1.17")))
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = targetVersion
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}
